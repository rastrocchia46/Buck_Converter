* ------------------------------------------------------------------------------
* 
* DISCLAIMER
* 
* INFINEON'S MODEL TERMS OF USE
* 
* BY DOWNLOADING AND/OR USING THIS INFINEON MODEL ("MODEL"), THE USER
* (INCLUDING YOU) AGREES TO BE BOUND BY THE TERMS OF USE HERE STATED. IF USER
* DOES NOT AGREE TO ALL TERMS OF USE HERE STATED, USER SHALL NOT DOWNLOAD,
* USE OR COPY THE MODEL BUT IMMEDIATELY DELETE IT (TO THE EXTENT THAT IT
* WAS DOWNLOADED ALREADY).
* 
* 1. SCOPE OF USE
* 1.1 Any use of this Model provided by Infineon Technologies AG is subject
*     to these Terms of Use.
* 1.2 This Model, provided by Infineon, does not fully represent all of the
*     specifications and operating characteristics of the product to which
*     this Model relates.
* 1.3 This Model only describes the characteristics of a typical product.
*     In all cases, the current data sheet information for a given product
*     is the final design guideline and the only actual performance
*     specification. Although this Model can be a useful tool to evaluate
*     the product performance, it cannot simulate the exact product performance
*     under all conditions and it is also not intended to replace
*     bread-boarding for final verification.
* 
* 2. IMPORTANT NOTICE
* 2.1 Infineon Technologies AG ("Infineon") is not and cannot be aware of the
*     specific application of the Infineon's Model by User. However, Model may
*     from time to time be used by User in potentially harmful and/or life-
*     endangering applications such as traffic, logistic, medical, nuclear
*     or military applications or in other applications where failure of the
*     Model may predictably cause damage to persons' life or health or to
*     property (hereinafter "Critical Applications").
* 2.2 User acknowledges that Infineon has not specifically designed or
*     qualified the Model for Critical Applications that the Model may contain
*     errors and bugs and that User is required to qualify the Model for
*     Critical Applications pursuant to the applicable local quality, safety
*     and legal requirements before permitting or giving access to any such use.
* 
* 3. CONFIDENTIAL INFORMATION
*     User shall treat ideas, concepts and information incorporated in the
*     Model, the documentation and the content of this Terms of Use (together
*     hereinafter "Confidential Information") confidential, not disclose it to
*     any third party unless otherwise agreed in writing between User and
*     Infineon, not use it for any other purposes than using the Model for
*     simulation and testing purposes only.
* 
* 4. WARRANTY
* 4.1 User acknowledges that the Model is provided by Infineon under this Terms
*     of Use is provided free of charge and "AS IS" without any warranty or
*     liability of any kind and Infineon hereby expressly disclaims any
*     warranties or representations, whether express, implied, statutory or
*     otherwise, including but not limited to warranties of workmanship,
*     merchantability, fitness for a particular purpose, defects in the
*     Model, or non-infringement of third parties intellectual property rights.
* 4.2 Infineon reserves the right to make corrections, deletions, modifications,
*     enhancements, improvements and other changes to the Model at any time
*     or to move or discontinue any Model without notice.
* 
* 5. LIABILITY
* 5.1 Nothing in this Terms of Use shall limit or exclude Infineon's liability
*     under mandatory liability laws, for injuries to life, body or health,
*     for fraudulent concealment of defects in the software, or in cases
*     of Infineon's intentional misconduct or gross negligence.
* 5.2 Without prejudice to Sections 5.1, in cases of Infineon's slight
*     negligent breach of obligations that restrict essential rights or duties
*     arising from the nature of this Terms of Use in a way that there is a
*     risk of non-achievement of the purpose of this Terms of Use or of an
*     obligation whose observance User regularly may trust in and whereas
*     compliance with only makes proper execution of this Terms of Use
*     possible, Infineon's liability shall be limited to the typically,
*     foreseeable damage.
* 5.3 Without prejudice to Sections 8.1 and 8.2, Infineon's liability under
*     this Agreement shall be excluded in all other cases.
* 
* 6. EXPORT REGULATIONS
*     The User shall comply with all applicable national and international
*     laws and regulations, in particular the applicable export control
*     regulations and sanction programs. The User also agrees not to
*     export, re-export or transfer any software or technology developed
*     with or using information, software or technology offered by Infineon,
*     in violation of any applicable laws or regulations of the competent
*     authorities. Further, the User shall neither use any products,
*     information, software and technology offered by Infineon in or in
*     connection with nuclear technology or weapons of mass destruction
*     (nuclear, biological or chemical) and carriers thereof nor supply
*     military consignees.
* 
* 7. TERMINATION OF USE PERMIT
*     If the User violates these Terms of Use, such User's permit to use
*     this Model terminates automatically. In addition, Infineon may
*     terminate the User's permit to use this Model at its discretion and
*     at any time regardless of any violation of these Terms of Use. In
*     any of the foregoing events, the User is obliged to immediately destroy
*     any content that has been downloaded or printed from Infineon's website.
* 
* 8. MISCELLANEOUS
* 8.1 These Terms of Use are subject to the laws of the Federal Republic
*     of Germany with the exception of the United Nations on Purchase
*     Contracts on the International Sale of Goods dated April 11, 1980 (CISG).
*     The exclusive place of jurisdiction is Munich, Germany.
* 8.2 Should any provision in these Terms of Use be or become invalid, the
*     validity of all other provisions or agreements shall remain unaffected
*     thereby.
* 
* ------------------------------------------------------------------------------
* 
* Title:    INFINEON Power Transistors Simulation Models for PSpice
* Description: n-channel Transistors
*                 OptiMOS7 100V
* Authors:  DES/MDL discretes modeling
* Email:    muc-des-discretes-modeling@infineon.com
									 

* Support:  Support@Infineon.com
*           www.Infineon.com/support
*
* ------------------------------------------------------------------------------
* 
* This library contains models of the following INFINEON transistors: 
* 
* Product          DeviceType  Level  VDSmax/V  RDSonmax/mOhm  IDmax/A  Package            L3-ModelName       L1-ModelName         L0-ModelName    
* -----------------------------------------------------------------------------------------------------------------------------------------------------
* IAUCN10S7N021      OptiMOS     NL      100        2.03         175    TDSON-8.(SSO8)     IAUCN10S7N021      IAUCN10S7N021_L1     IAUCN10S7N021_L0
* IAUCN10S7N040      OptiMOS     NL      100        4.03         120    TDSON-8.(SSO8)     IAUCN10S7N040      IAUCN10S7N040_L1     IAUCN10S7N040_L0
* IAUCN10S7N074      OptiMOS     NL      100        7.38          70    TDSON-8.(SSO8)     IAUCN10S7N074      IAUCN10S7N074_L1     IAUCN10S7N074_L0
* IAUZN10S7N078      OptiMOS     NL      100	    7.81          60    TDSON-8.(SSO8)     IAUCN10S7N078      IAUCN10S7N078_L1     IAUCN10S7N078_L0
* IAUCN10S7L040      OptiMOS     LL      100        3.95         120    TDSON-8.(SSO8)     IAUCN10S7L040      IAUCN10S7L040_L1     IAUCN10S7L040_L0
* IAUCN10S7L180      OptiMOS     LL      100       18.02          35    TDSON-8.(SSO8)     IAUCN10S7L180      IAUCN10S7L180_L1     IAUCN10S7L180_L0
* IAUZN10S7L289      OptiMOS     LL      100       28.86          26    TDSON-8.(S3O8)     IAUZN10S7L289      IAUZN10S7L289_L1     IAUZN10S7L289_L0 
* ------------------------------------------------------------------------------
* 
* Thermal nodes of level 3 models: 
* 
*  Tj:    Potential (temperature in °C) at junction (typically not connected) 
*  Tcase: Node where boundary condition (external heat sinks) must be connected.
*         Ideal heat sink can be modeled by using a voltage source stating the 
*         ambient temperature in °C between Tcase and ground. 
* 
* ------------------------------------------------------------------------------
* 
* This file also contains simplified models that are compatible to standard 
* Spice (Level 0) 
* 
* ------------------------------------------------------------------------------
* History 
* DD/MM/YY 
* 22/11/23 initial version based on "OptiMOS7_40V.lib"                    (Koch)
* 11/12/24 Gate charge improvement on l7 tech core			  (Goel)
* 06/02/25 Update in 100V LL and NL of Level 0                  (Martinez, Goel) 
* 13/02/25 Added SSO8 products:                                         (Scholz)
*                IAUCN10S7N040, IAUCN10S7N074, IAUCN10S7L180
* 18/02/25 Added IAUCN10S7N021                                        (Martinez)
* 04/04/25 Added IAUZN10S7N078, IAUCN10S7L289                           (Scholz)
* 07/07/25 Added IAUCN10S7L040                                          (Scholz)
* ------------------------------------------------------------------------------
* 
* A7_100_n7_var: AFET7_100 NL 57um
* A7_100_l7_var: AFET7_100 LL 57um
* 
* ------------------------------------------------------------------------------
*
* turning off LTspice`s default 1 mOhm series damping resistance (R=0)
.options Thev_Induc=1
*
********************************************************************************
* Technology Models
********************************************************************************

.SUBCKT A7_100_n7_var dd g s0 sp Tj PARAMS: a=1 dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 
+ Inn=1 Unn=1 Rmax=0 gmin=1 Rsp=1 Rs=1 Rp=1 Rmet=1u

* corner parameters
.PARAM  dVthmax=0.4   dCmax=0.3   dVbrmax=3.15
.PARAM  dVthmin=0.5

* technology parameters
.PARAM  Fm=0.05    Fn=0.5      al=0.5
.PARAM  c=0.95     Vth0=4.0    auth=3.9m
.PARAM  dvx=550m   dvgs=0.26   auth_sub=3.63m
.PARAM  UT=100m    ab=37.3m    lB=-23       UB=110.5

.PARAM  b0=68.0    p0=5.23     p1=-10.3m    p2=18.6u

.PARAM  Rd=19.5m   nmu=3.07    Tref=298     T0=273      lnIsj=-26.09
.PARAM  ndi=1.164  nisj=2.74   Rdi=3.78m    nmu2=1.266
.PARAM  td=20n     ta=2n
.PARAM  Rf=460m    nmu3=1.7

.PARAM  kbq=86.2u

* Cgs
.PARAM  f3=357p    rc1=0

* Cgfp
.PARAM  f3a=70p

* Cds
.PARAM  qs1=50.55p  qs2=244.0p  qs3=-24.78m
.PARAM  qs4=41.35p  qs5=-0.940
.PARAM  q81=81.51p  f2r=0
.PARAM  x0=28.9     x1=85.7     dx={x1-x0}

* Cgd
.PARAM  ps0=42.05p  ps1=27.45p  ps2=-89.37m
.PARAM  ps3=19.97p  ps4=-0.698  ps5=0.302p
.PARAM  ps6=5.025p  f5=1.478p
.PARAM  x2=0        x3=84.34    dx2={x3-x2}


.PARAM  Vth={Vth0+dVthmax*limit(dVth,0,1)+dVthmin*limit(dVth,-1,0)}
.PARAM  q0={b0*((T0/Tref)**nmu3)*a}
.PARAM  q1={(Unn-Inn*Rs-Vth0)*q0}
.PARAM  q2={(Fm*sqrt(0.4)-c)*Inn*q0}
.PARAM  Rlim={(q1+2*q2*Rmax-sqrt(max(q1**2+4*q2,0)))/(2*q2)}
.PARAM  dRd={Rd/a+if(dVth==0,limit(dRdson,0,1)*max(Rlim-Rd/a-Rs-Rp,0),0)}
.PARAM  bm={c/((1/gmin-Rs)**2*Inn*a*(T0/Tref)**nmu3)}
.PARAM  bet={b0+(b0-bm)*if(dRdson==0,if(dVth==0,limit(dgfs,-1,0),0),0)}
.PARAM  dC1={1+dCmax*limit(dC,0,1)}

.PARAM  Cgs0={(f3*a+rc1)*dC1}
.PARAM  Cgs1={f3a*a*dC1}
.PARAM  dRdi={Rdi/a}

.PARAM  Cox1={(ps1*a+ps0*sqrt(a))*dC1}
.PARAM  Cox2={ps3*a*dC1}
.PARAM  Cox3={(ps5*a+ps6)*dC1}
.PARAM  Cox4={(f5*a+(ps5*a+ps6))*dC1}

.PARAM  Cds0={qs1*a*dC1}
.PARAM  Cds1={qs2*(1+f2r/sqrt(a))*a*dC1}
.PARAM  Cds2={qs4*a*dC1}
.PARAM  Cds3={(q81+qs1)*a*dC1}

.FUNC  VBR(Usps)          {UB+dVbrmax*dVbr}

.FUNC  Ue(g,y,w)          {(g-Vth+auth*(w-Tref)+Fm*y**Fn)}
.FUNC  Ue1(g,y,w)         {Ue(g,y,w)+(1+limit(Ue(g,y,w)+dvx,0,1)**2*(2*limit(Ue(g,y,w)+dvx,0,1)-3))*(dvgs+(auth_sub-auth)*(w-Tref))}

.FUNC  I0(Uee,p,pp,z1)    {if(Uee>pp,(Uee-c*z1)*z1,p*(pp-p)/c*exp((Uee-pp)/p))}
.FUNC  Ig(Uds,T,p,Uee)    {bet*(T0/T)**nmu3*I0(Uee,p,min(2*p,p+c*Uds),min(Uds,Uee/(2*c)))}
.FUNC  J(d,g,T,da,s,Usps) 
+ {a*(s*(Ig(da,T,(p0+(p1+p2*T)*T)*kbq*T,Ue1(g,da,T))+
+ exp(min(lB+(d-VBR(Usps)-ab*(T-Tref))/UT,25))))}

.FUNC  Idiode(Usd,Tj,Iss) {exp(min(log(Iss)+Usd/(ndi*kbq*Tj),7))-Iss}
.FUNC  Idiod(Usd,Tj)      {a*Idiode(Usd,Tj,exp(min(lnIsj+(Tj/Tref-1)*1.12/(ndi*kbq*Tj),7))*(Tj/Tref)**nisj)}

.FUNC  Pr(Vss0,Vssp)      {Vss0*Vss0/max(Rmet,1u)+Vssp*Vssp/Rsp}

.FUNC QCds(x) {Cds3*min(x,x1)+Cds0*max(x-x1,0)+(Cds3-Cds0)*((limit(x,x0,x1)-x0)**3/(dx*dx)*((limit(x,x0,x1)-x0)/(2*dx)-1))}
.FUNC QCdg(x) {Cox4*min(x,x3)+Cox3*max(x-x3,0)+(Cox4-Cox3)*((limit(x,x2,x3)-x2)**3/(dx2*dx2)*((limit(x,x2,x3)-x2)/(2*dx2)-1))}

.param vdsx=-3 
.Func smooth(x)  {0.5 * (x + vdsx + sqrt((x - vdsx) * (x - vdsx)+0.001)) }
E_Edg1     d   ox    VALUE {smooth(V(d,g))-(exp(ps2*smooth(V(d,g)))-(1-ps2*vdsx)*exp(ps2*vdsx))/ps2/exp(ps2*vdsx)}
C_Cdg1    ox   g     {Cox1*exp(ps2*vdsx)}
E_Edg2     d   ox1   VALUE {smooth(V(d,g))-(exp(ps4*smooth(V(d,g)))-(1-ps4*vdsx)*exp(ps4*vdsx))/ps4/exp(ps4*vdsx)}
C_Cdg2   ox1   g     {Cox2*exp(ps4*vdsx)}
E_Edg3     d   ox2   VALUE {V(d,g)-QCdg(V(d,g))/Cox4}
C_Cdg3   ox2   g     {Cox4}


E_Eds      d   edep  VALUE {V(d,s)-QCds(V(d,s))/Cds3}
C_Cds   edep   s     {Cds3/2} 

E_Eds1     d   edep1 VALUE {V(d,sp)-QCds(V(d,sp))/Cds3}
C_Cds1 edep1   sp    {Cds3/2} 
E_Eds2     d   edep2 VALUE {if(V(d,sp)>0,V(d,sp)-(exp(qs5*max(V(d,sp),0))-1)/qs5,0)}
C_Cds2 edep2   sp    {Cds2}
E_Eds3     d   edep3 VALUE {if(V(d,sp)>0,V(d,sp)-(exp(qs3*max(V(d,sp),0))-1)/qs3,0)}
C_Cds3 edep3   sp    {Cds1}

C_Cgs      g   s     {Cgs0}
C_Cgs1     g   sp    {Cgs1}

R_fp       s   sp    {Rsp}

G_chan   d5a   s     VALUE={J(V(d5a,s),V(g,s),T0+limit(V(Tj),-200,300),(sqrt(1+4*al*abs(V(d5a,s)))-1)/2/al,sgn(V(d5a,s)),V(sp,s))}
R_d06    d5a   d5    1u
V_sm       d   d5    0
G_RMos    d1   d     VALUE={V(d1,d)/(Rf*dRd+(1-Rf)*dRd*((limit(V(Tj),-200,999)+T0)/Tref)**nmu)}
V_sense   dd   d1    0
G_diode    s   d3    VALUE={Idiod(V(s,d3),T0+limit(V(Tj),-200,499))}
G_Rdio    d2   d1    VALUE={V(d2,d1)/(dRdi*((limit(V(Tj),-200,999)+T0)/Tref)**nmu2)}
V_sense2  d2   d3    0

R_1        g   s     1G
R_d01      d   s     1G
R_d02     d2   s     1G
R_d03     d1   d     1k
R_ssp      g   sp    100Meg

R_met      s   s0    {Rmet}

G_th       0   Tj    VALUE={(I(V_sense)-I(V_sense2))*V(d1,d)+I(V_sm)*V(d,s)+I(V_sense2)*V(d1,s)+Pr(V(s,s0),V(s,sp))}

.ENDS

**********

.SUBCKT A7_100_l7_var dd g s0 sp Tj PARAMS: a=1 dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 
+ Inn=1 Unn=1 Rmax=0 gmin=1 Rsp=1 Rs=1 Rp=1 Rmet=1u

* corner parameters
.PARAM  dVthmax=0.4   dCmax=0.3   dVbrmax=3.15

* technology parameters
.PARAM  Fm=0.05    Fn=0.5      al=0.5
.PARAM  c=1.5      Vth0=2.4    auth=3.03m
.PARAM  dvx=550m   dvgs=0.125  auth_sub=3.34m
.PARAM  UT=100m    ab=36.8m    lB=-23       UB=111.6

.PARAM  b0=160     p0=5.478    p1=-15.89m   p2=22.79u

.PARAM  Rd=21m     nmu=3.05    Tref=298     T0=273      lnIsj=-26.45
.PARAM  ndi=1.075   nisj=2.826   Rdi=6.36m     nmu2=0.903
.PARAM  td=20n     ta=2n
.PARAM  Rf=450m    nmu3=1.5

.PARAM  kbq=86.2u

* Cgs
.PARAM  f3=395p    rc1=15p

* Cgfp
.PARAM  f3a=70p

* Cds
.PARAM  qs1=43.54p  qs2=181.5p  qs3=-21.45m
.PARAM  qs4=42.42p  qs5=-1.003
.PARAM  q81=73.8p   f2r=0.61
.PARAM  x0=31.4     x1=84.1     dx={x1-x0}

* Cgd
.PARAM  ps0=40p   ps1=30p   ps2=-0.8
.PARAM  ps3=32p   ps4=-65m  ps5=450f
.PARAM  ps6=3.9p    f5=7.526p
.PARAM  x2=501.7u   x3=21.20    dx2={x3-x2}

.PARAM  Vth={Vth0+dVthmax*dVth}
.PARAM  q0={b0*((T0/Tref)**nmu3)*a}
.PARAM  q1={(Unn-Inn*Rs-Vth0)*q0}
.PARAM  q2={(Fm*sqrt(0.4)-c)*Inn*q0}
.PARAM  Rlim={(q1+2*q2*Rmax-sqrt(max(q1**2+4*q2,0)))/(2*q2)}
.PARAM  dRd={Rd/a+if(dVth==0,limit(dRdson,0,1)*max(Rlim-Rd/a-Rs-Rp,0),0)}
.PARAM  bm={c/((1/gmin-Rs)**2*Inn*a*(T0/Tref)**nmu3)}
.PARAM  bet={b0+(b0-bm)*if(dRdson==0,if(dVth==0,limit(dgfs,-1,0),0),0)}
.PARAM  dC1={1+dCmax*limit(dC,0,1)}

.PARAM  Cgs0={(f3*a+rc1)*dC1}
.PARAM  Cgs1={f3a*a*dC1}
.PARAM  dRdi={Rdi/a}

.PARAM  Cox1={(ps1*a+ps0*sqrt(a))*dC1}
.PARAM  Cox2={ps3*a*dC1}
.PARAM  Cox3={(ps5*a+ps6)*dC1}
.PARAM  Cox4={(f5*a+(ps5*a+ps6))*dC1}

.PARAM  Cds0={qs1*a*dC1}
.PARAM  Cds1={qs2*(1+f2r/sqrt(a))*a*dC1}
.PARAM  Cds2={qs4*a*dC1}
.PARAM  Cds3={(q81+qs1)*a*dC1}

.FUNC  VBR(Usps)          {UB+dVbrmax*dVbr}

.FUNC  Ue(g,y,w)          {(g-Vth+auth*(w-Tref)+Fm*y**Fn)}
.FUNC  Ue1(g,y,w)         {Ue(g,y,w)+(1+limit(Ue(g,y,w)+dvx,0,1)**2*(2*limit(Ue(g,y,w)+dvx,0,1)-3))*(dvgs+(auth_sub-auth)*(w-Tref))}

.FUNC  I0(Uee,p,pp,z1)    {if(Uee>pp,(Uee-c*z1)*z1,p*(pp-p)/c*exp((Uee-pp)/p))}
.FUNC  Ig(Uds,T,p,Uee)    {bet*(T0/T)**nmu3*I0(Uee,p,min(2*p,p+c*Uds),min(Uds,Uee/(2*c)))}
.FUNC  J(d,g,T,da,s,Usps) 
+ {a*(s*(Ig(da,T,(p0+(p1+p2*T)*T)*kbq*T,Ue1(g,da,T))+
+ exp(min(lB+(d-VBR(Usps)-ab*(T-Tref))/UT,25))))}

.FUNC  Idiode(Usd,Tj,Iss) {exp(min(log(Iss)+Usd/(ndi*kbq*Tj),7))-Iss}
.FUNC  Idiod(Usd,Tj)      {a*Idiode(Usd,Tj,exp(min(lnIsj+(Tj/Tref-1)*1.12/(ndi*kbq*Tj),7))*(Tj/Tref)**nisj)}

.FUNC  Pr(Vss0,Vssp)      {Vss0*Vss0/max(Rmet,1u)+Vssp*Vssp/Rsp}

.FUNC QCds(x) {Cds3*min(x,x1)+Cds0*max(x-x1,0)+(Cds3-Cds0)*((limit(x,x0,x1)-x0)**3/(dx*dx)*((limit(x,x0,x1)-x0)/(2*dx)-1))}
.FUNC QCdg(x) {Cox4*min(x,x3)+Cox3*max(x-x3,0)+(Cox4-Cox3)*((limit(x,x2,x3)-x2)**3/(dx2*dx2)*((limit(x,x2,x3)-x2)/(2*dx2)-1))}

.param vdsx=-2
.Func smooth(x)  {0.5 * (x + vdsx + sqrt((x - vdsx) * (x - vdsx)+0.001)) }
E_Edg1     d   ox    VALUE {smooth(V(d,g))-(exp(ps2*smooth(V(d,g)))-(1-ps2*vdsx)*exp(ps2*vdsx))/ps2/exp(ps2*vdsx)}
C_Cdg1    ox   g     {Cox1*exp(ps2*vdsx)}
E_Edg2     d   ox1   VALUE {smooth(V(d,g))-(exp(ps4*smooth(V(d,g)))-(1-ps4*vdsx)*exp(ps4*vdsx))/ps4/exp(ps4*vdsx)}
C_Cdg2   ox1   g     {Cox2*exp(ps4*vdsx)}
E_Edg3     d   ox2   VALUE {V(d,g)-QCdg(V(d,g))/Cox4}
C_Cdg3   ox2   g     {Cox4}


E_Eds      d   edep  VALUE {V(d,s)-QCds(V(d,s))/Cds3}
C_Cds   edep   s     {Cds3/2} 

E_Eds1     d   edep1 VALUE {V(d,sp)-QCds(V(d,sp))/Cds3}
C_Cds1 edep1   sp    {Cds3/2} 
E_Eds2     d   edep2 VALUE {if(V(d,sp)>0,V(d,sp)-(exp(qs5*max(V(d,sp),0))-1)/qs5,0)}
C_Cds2 edep2   sp    {Cds2}
E_Eds3     d   edep3 VALUE {if(V(d,sp)>0,V(d,sp)-(exp(qs3*max(V(d,sp),0))-1)/qs3,0)}
C_Cds3 edep3   sp    {Cds1}

C_Cgs      g   s     {Cgs0}
C_Cgs1     g   sp    {Cgs1}

R_fp       s   sp    {Rsp}

G_chan   d5a   s     VALUE={J(V(d5a,s),V(g,s),T0+limit(V(Tj),-200,300),(sqrt(1+4*al*abs(V(d5a,s)))-1)/2/al,sgn(V(d5a,s)),V(sp,s))}
R_d06    d5a   d5    1u
V_sm       d   d5    0
G_RMos    d1   d     VALUE={V(d1,d)/(Rf*dRd+(1-Rf)*dRd*((limit(V(Tj),-200,999)+T0)/Tref)**nmu)}
V_sense   dd   d1    0
G_diode    s   d3    VALUE={Idiod(V(s,d3),T0+limit(V(Tj),-200,499))}
G_Rdio    d2   d1    VALUE={V(d2,d1)/(dRdi*((limit(V(Tj),-200,999)+T0)/Tref)**nmu2)}
V_sense2  d2   d3    0

R_1        g   s     1G
R_d01      d   s     1G
R_d02     d2   s     1G
R_d03     d1   d     1k
R_ssp      g   sp    100Meg

R_met      s   s0    {Rmet}

G_th       0   Tj    VALUE={(I(V_sense)-I(V_sense2))*V(d1,d)+I(V_sm)*V(d,s)+I(V_sense2)*V(d1,s)+Pr(V(s,s0),V(s,sp))}

.ENDS



********************************************************************************
* Level 3 Models
********************************************************************************


.SUBCKT IAUCN10S7N021 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.3n Ld=0.05n Lg=3n

.PARAM Rs=91u       Rg=2.2       Rd=4u        Rmet=12u
.PARAM Inn=87.5     Unn=10       Rmax=2.03m   gmin=94
.PARAM act=12.99    Rsp=0.94

X_1  d1 g s sp Tj A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                               Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {2.53m+limit(dZth,0,1)*930.86u}
R_th2  t1      t2              {27.38m+limit(dZth,0,1)*10.14m}
R_th3  t2      t3              {29.19m+limit(dZth,0,1)*3.42m}
R_th4  t3      t4              {67.72m+limit(dZth,0,1)*75.24m}
R_th5  t4      Tcase           {224.27m+limit(dZth,0,1)*249.18m}
C_th1  Tj      0               180.85u
C_th2  t1      0               416.276u
C_th3  t2      0               948.69u
C_th4  t3      0               2.289m
C_th5  t4      0               46.675m
C_th6  Tcase   0               30m

.ENDS

**********

.SUBCKT IAUCN10S7N040 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=196u      Rg=1.3       Rd=5u        Rmet=66u
.PARAM Inn=60       Unn=10       Rmax=4.03m   gmin=55
.PARAM act=6.56     Rsp=0.55

X_1  d1 g s sp Tj A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                               Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {5m+limit(dZth,0,1)*1.85m}
R_th2  t1      t2              {53.25m+limit(dZth,0,1)*19.71m}
R_th3  t2      t3              {54.42m+limit(dZth,0,1)*7.13m}
R_th4  t3      t4              {131.2m+limit(dZth,0,1)*137.37m}
R_th5  t4      Tcase           {317.57m+limit(dZth,0,1)*332.5m}
C_th1  Tj      0               91.33u
C_th2  t1      0               214.074u
C_th3  t2      0               490.531u
C_th4  t3      0               1.156m
C_th5  t4      0               30.6m
C_th6  Tcase   0               30m

.ENDS

**********

.SUBCKT IAUCN10S7N074 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=152u      Rg=2.5       Rd=6u        Rmet=24u
.PARAM Inn=35       Unn=10       Rmax=7.38m   gmin=31
.PARAM act=3.49     Rsp=1.1

X_1  d1 g s sp Tj A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                               Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {9.4m+limit(dZth,0,1)*3.48m}
R_th2  t1      t2              {97.81m+limit(dZth,0,1)*36.2m}
R_th3  t2      t3              {95.05m+limit(dZth,0,1)*14.25m}
R_th4  t3      t4              {237.47m+limit(dZth,0,1)*220.47m}
R_th5  t4      Tcase           {438.63m+limit(dZth,0,1)*407.24m}
C_th1  Tj      0               48.589u
C_th2  t1      0               116.545u
C_th3  t2      0               268.876u
C_th4  t3      0               615.004u
C_th5  t4      0               22.925m
C_th6  Tcase   0               30m

.ENDS

**********

.SUBCKT IAUZN10S7N078 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.2n Ld=0.05n Lg=2n

.PARAM Rs=167u      Rg=0.71      Rd=3u        Rmet=45u
.PARAM Inn=30       Unn=10       Rmax=7.81m   gmin=28
.PARAM act=3.29     Rsp=0.3


X_1  d1 g s sp Tj A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                     Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {9.99m+limit(dZth,0,1)*3.7m}
R_th2  t1      t2              {103.64m+limit(dZth,0,1)*38.36m}
R_th3  t2      t3              {100.13m+limit(dZth,0,1)*15.15m}
R_th4  t3      t4              {251.05m+limit(dZth,0,1)*223.19m}
R_th5  t4      Tcase           {452.5m+limit(dZth,0,1)*402.29m}
C_th1  Tj      0               45.804u
C_th2  t1      0               110.145u
C_th3  t2      0               254.299u
C_th4  t3      0               579.761u
C_th5  t4      0               22.425m
C_th6  Tcase   0               10m

.ENDS

**********
.SUBCKT IAUCN10S7L040 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=196u      Rg=1.3       Rd=5u        Rmet=66u
.PARAM Inn=60       Unn=10       Rmax=3.95m   gmin=55
.PARAM act=6.56     Rsp=0.55

X_1  d1 g s sp Tj A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                     Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {5m+limit(dZth,0,1)*1.85m}
R_th2  t1      t2              {53.25m+limit(dZth,0,1)*19.71m}
R_th3  t2      t3              {54.42m+limit(dZth,0,1)*7.13m}
R_th4  t3      t4              {131.2m+limit(dZth,0,1)*137.37m}
R_th5  t4      Tcase           {317.57m+limit(dZth,0,1)*332.5m}
C_th1  Tj      0               91.33u
C_th2  t1      0               214.074u
C_th3  t2      0               490.531u
C_th4  t3      0               1.156m
C_th5  t4      0               30.6m
C_th6  Tcase   0               30m

.ENDS

********

.SUBCKT IAUCN10S7L180 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=275u      Rg=2.8       Rd=8u        Rmet=103u
.PARAM Inn=17.5     Unn=10       Rmax=18.02m  gmin=14
.PARAM act=1.39     Rsp=1.2

X_1  d1 g s sp Tj A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                               Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {23.61m+limit(dZth,0,1)*8.74m}
R_th2  t1      t2              {233.86m+limit(dZth,0,1)*86.55m}
R_th3  t2      t3              {206.59m+limit(dZth,0,1)*40.11m}
R_th4  t3      t4              {532.54m+limit(dZth,0,1)*314.42m}
R_th5  t4      Tcase           {719.05m+limit(dZth,0,1)*424.53m}
C_th1  Tj      0               19.352u
C_th2  t1      0               48.744u
C_th3  t2      0               114.052u
C_th4  t3      0               244.944u
C_th5  t4      0               17.675m
C_th6  Tcase   0               30m

.ENDS

**********

.SUBCKT IAUZN10S7L289 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 dZth=0 Ls=0.4n Ld=0.05n Lg=2n

.PARAM Rs=793u      Rg=1.2       Rd=8u        Rmet=243u
.PARAM Inn=13       Unn=10       Rmax=28.86m  gmin=9
.PARAM act=0.877    Rsp=0.5

X_1  d1 g s sp Tj A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                               Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

R_th1  Tj      t1              {37.43m+limit(dZth,0,1)*13.86m}
R_th2  t1      t2              {358.52m+limit(dZth,0,1)*132.68m}
R_th3  t2      t3              {299.01m+limit(dZth,0,1)*68.06m}
R_th4  t3      t4              {764.85m+limit(dZth,0,1)*300.37m}
R_th5  t4      Tcase           {951.53m+limit(dZth,0,1)*373.69m}
C_th1  Tj      0               12.21u
C_th2  t1      0               31.806u
C_th3  t2      0               75.126u
C_th4  t3      0               154.544u
C_th5  t4      0               16.393m
C_th6  Tcase   0               10m

.ENDS



********************************************************************************
* Level 1 Models
********************************************************************************

.SUBCKT IAUCN10S7N021_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.3n Ld=0.05n Lg=3n

.PARAM Rs=91u       Rg=2.2       Rd=4u        Rmet=12u
.PARAM Inn=87.5     Unn=10       Rmax=2.03m   gmin=94
.PARAM act=12.99    Rsp=0.94

X_1  d1 g s sp Tj1 A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUCN10S7N040_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=196u      Rg=1.3       Rd=5u        Rmet=66u
.PARAM Inn=60       Unn=10       Rmax=4.03m   gmin=55
.PARAM act=6.56     Rsp=0.55

X_1  d1 g s sp Tj1 A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUCN10S7N074_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=152u      Rg=2.5       Rd=6u        Rmet=24u
.PARAM Inn=35       Unn=10       Rmax=7.38m   gmin=31
.PARAM act=3.49     Rsp=1.1

X_1  d1 g s sp Tj1 A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUCN10S7L040_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=196u      Rg=1.3       Rd=5u        Rmet=66u
.PARAM Inn=60       Unn=10       Rmax=3.95m   gmin=55
.PARAM act=6.56     Rsp=0.55

X_1  d1 g s sp Tj1 A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUZN10S7N078_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.2n Ld=0.05n Lg=2n

.PARAM Rs=167u      Rg=0.71      Rd=3u        Rmet=45u
.PARAM Inn=30       Unn=10       Rmax=7.81m   gmin=28
.PARAM act=3.29     Rsp=0.3

X_1  d1 g s sp Tj1 A7_100_n7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                      Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUCN10S7L180_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.4n Ld=0.05n Lg=3n

.PARAM Rs=275u      Rg=2.8       Rd=8u        Rmet=103u
.PARAM Inn=17.5     Unn=10       Rmax=18.02m  gmin=14
.PARAM act=1.39     Rsp=1.2

X_1  d1 g s sp Tj1 A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS

**********

.SUBCKT IAUZN10S7L289_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dVbr=0 Ls=0.4n Ld=0.05n Lg=2n

.PARAM Rs=793u      Rg=1.2       Rd=8u        Rmet=243u
.PARAM Inn=13       Unn=10       Rmax=28.86m  gmin=9
.PARAM act=0.877    Rsp=0.5

X_1  d1 g s sp Tj1 A7_100_l7_var PARAMS: a={act} dVth={dVth} dRdson={dRdson} dgfs={dgfs} dC={dC} dVbr={dVbr} 
+                                Inn={Inn} Unn={Unn} Rmax={Rmax} gmin={gmin} Rsp={Rsp} Rs={Rs} Rp={Rd} Rmet={Rmet}
R_g    g1     g    {Rg}
L_g    gate   g1   {Lg*if(dgfs==99,0,1)}
G_s    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)}
R_sa   s1     s    1Meg
L_s    source s1   {Ls*if(dgfs==99,0,1)}
R_da   d1     d2   {Rd}
L_d    drain  d2   {Ld*if(dgfs==99,0,1)}
R_daux drain  d2   10
R_gaux gate   g1   10
R_saux source s1   10

E_2    Tj     w    VALUE={TEMP}
V_p    Tj1    Tj   0
R_1    Tj     Tj1  1u
G_power  0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rmet)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(V_p)}
R_2      w    0    1u

.ENDS


********************************************************************************
* Level 0 Models
********************************************************************************

.SUBCKT IAUCN10S7N021_L0  drain  gate  source

L_g      gate     g1   3n
L_d      drain    d1   0.05n
L_s      source   s1   0.3n
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
R_s      s1       s2   91u   TC=3m

R_g      g1   g2   2.2
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=829  VTO=4.18  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   1.58m   TC=7.38m

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=108  M=2.075  CJO=20n  VJ=52 )
R_sp     s2   s3   0.94
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.075  CJO=29.32n  VJ=52 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=71.6p  N=1.161  RS=0.04u  EG=1.12  TT=20n )
R_diode  d1   21   0.36m   TC=3.77m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   1.31n
.MODEL   DGD  D ( M=1.664   CJO=1.31n   VJ=5.71 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   5.52n

.ENDS  IAUCN10S7N021_L0

******

.SUBCKT IAUCN10S7N040_L0  drain  gate  source

L_g      gate     g1   3n
L_d      drain    d1   0.05n
L_s      source   s1   0.4n
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
R_s      s1       s2   196u   TC=3m

R_g      g1   g2   1.3
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=418.7  VTO=4.18  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   3.12m   TC=7.38m

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=108  M=2.075  CJO=10.1n  VJ=52 )
R_sp     s2   s3   0.55
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.075  CJO=14.86n  VJ=52 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=36.1p  N=1.161  RS=0.08u  EG=1.12  TT=20n )
R_diode  d1   21   0.72m   TC=3.77m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.85n
.MODEL   DGD  D ( M=1.664   CJO=0.85n   VJ=5.71 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   2.79n

.ENDS  IAUCN10S7N040_L0

******

.SUBCKT IAUCN10S7N074_L0  drain  gate  source

L_g      gate     g1   3n
L_d      drain    d1   0.05n
L_s      source   s1   0.4n
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
R_s      s1       s2   152u   TC=3m

R_g      g1   g2   2.5
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=222.7  VTO=4.18  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   5.87m   TC=7.38m

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=108  M=2.075  CJO=5.37n  VJ=52 )
R_sp     s2   s3   1.1
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.075  CJO=7.94n  VJ=52 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=19.2p  N=1.161  RS=0.14u  EG=1.12  TT=20n )
R_diode  d1   21   1.35m   TC=3.77m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.58n
.MODEL   DGD  D ( M=1.664   CJO=0.58n   VJ=5.71 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   1.49n

.ENDS  IAUCN10S7N074_L0


******

.SUBCKT IAUZN10S7N078_L0  drain  gate  source

L_g      gate     g1   2n
L_d      drain    d1   0.05n
L_s      source   s1   0.2n
R_s      s1       s2   167u   TC=3m
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10

R_g      g1   g2   0.71
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=210  VTO=4.18  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   6.22m   TC=7.38m

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=108  M=2.075  CJO=5.07n  VJ=52 )
R_sp     s2   s3   0.3
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.075  CJO=7.48n  VJ=52 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=18.1p  N=1.161  RS=0.15u  EG=1.12  TT=20n )
R_diode  d1   21   1.44m   TC=3.77m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.56n
.MODEL   DGD  D ( M=1.664   CJO=0.56n   VJ=5.71 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   1.4n

.ENDS  IAUZN10S7N078_L0

******

.SUBCKT IAUCN10S7L040_L0  drain  gate  source

L_g      gate     g1   3n
L_d      drain    d1   0.05n
L_s      source   s1   0.4n
R_s      s1       s2   196u   TC=3m
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
					  			  
R_g      g1   g2   1.3
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=605.2  VTO=2.473  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   3.11m   TC=0.00874

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=113.5  M=2.229  CJO=9.77n  VJ=56.2 )
R_sp     s2   s3   0.55
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.229  CJO=38.95n  VJ=56.2 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=41.3p  N=1.08  RS=0.08u  EG=1.12  TT=3n )
R_diode  d1   21   1.3m   TC=2.2m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.4n
.MODEL   DGD  D ( M=1.414   CJO=0.4n   VJ=6.847 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   3.03n

.ENDS  IAUCN10S7L040_L0

******

.SUBCKT IAUCN10S7L180_L0  drain  gate  source

L_g      gate     g1   3n
L_d      drain    d1   0.05n
L_s      source   s1   0.4n
R_s      s1       s2   275u   TC=3m
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
R_g      g1   g2   2.8
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=128.2  VTO=2.473  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   14.67m   TC=0.00874

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=113.5  M=2.229  CJO=2.07n  VJ=56.2 )
R_sp     s2   s3   1.2
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.229  CJO=9.51n  VJ=56.2 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=8.7p  N=1.08  RS=0.36u  EG=1.12  TT=3n )
R_diode  d1   21   6.12m   TC=2.2m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.15n
.MODEL   DGD  D ( M=1.414   CJO=0.15n   VJ=6.847 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   0.66n

.ENDS  IAUCN10S7L180_L0

******

.SUBCKT IAUZN10S7L289_L0  drain  gate  source

L_g      gate     g1   2n
L_d      drain    d1   0.05n
L_s      source   s1   0.4n
R_s      s1       s2   793u   TC=3m
R_daux   drain    d1   10
R_gaux   gate     g1   10
R_saux   source   s1   10
R_g      g1   g2   1.2
M_1      d2   g2   s2   s2   DMOS   L=1u   W=1u
.MODEL   DMOS NMOS ( KP=80.9  VTO=2.473  THETA=0  VMAX=1.5e5  ETA=0.005  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=3 )
R_d      d1   d2   23.25m   TC=0.00874

D_bd     s2   d2   Dbt
.MODEL   Dbt   D ( BV=113.5  M=2.229  CJO=1.31n  VJ=56.2 )
R_sp     s2   s3   0.5
D_bd1    s3   d2   Dbt1
.MODEL   Dbt1  D ( BV=1000  M=2.229  CJO=6.38n  VJ=56.2 )
D_body   s2   21   DBODY
.MODEL   DBODY D ( IS=5.5p  N=1.08  RS=0.57u  EG=1.12  TT=3n )
R_diode  d1   21   9.69m   TC=2.2m

.MODEL   sw   NMOS ( VTO=0  KP=10  LAMBDA=1m  CGDO=1f  CGSO=1f  CGBO=1f  IS=0  LEVEL=1 )
M_aux    g2   c    a    a    sw   L=10u   W=10u
M_aux2   b    d    g2   g2   sw   L=10u   W=10u
E_aux    c    a    d2   g2   1
E_aux2   d    g2   d2   g2   -1
C_ox     b    d2   0.11n
.MODEL   DGD  D ( M=1.414   CJO=0.11n   VJ=6.847 )
* R_par    b    d2   1Meg
D_gd     a    d2   DGD
* R_par2   d2   a    10Meg
C_gs     g2   s2   0.43n

.ENDS  IAUZN10S7L289_L0


******
